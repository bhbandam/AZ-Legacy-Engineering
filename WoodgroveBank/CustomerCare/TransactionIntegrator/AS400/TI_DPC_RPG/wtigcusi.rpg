      ******************************************************************
      * File Definitions
      ******************************************************************
      *
     FCusMasl1  if   e           k DISK
      *
      ******************************************************************
      * Data Definitions
      ******************************************************************
     D Name            s             30A
     D RespCode        S              9S 0 INZ(*ZEROS)
     D RetCode         S              4  0 INZ(*ZEROS)
     D EditNum         S              7S 0
     D msg             S             45A   Dim(3) ctdata
      *
     d pRetCode        s              4  0
     d pCustName       s             30A
     d pCustomerName   s             30A
      *
      ******************************************************************
      * Customer Detail
      ******************************************************************
      *
     d pUserData       ds
     d  pCustomerSSN                  9A
     d  pCustomerStr                 20a
     d  pCustomerCity                10A
     d  pCustomerSta                  4A
     d  pCustomerZip                 10i 0
     d  pCustomerPhne                13A
     d  pCustomerPin                  4A
      *
      ******************************************************************
      * Metadata Block
      ******************************************************************
      *
     d pMetaDtaErrBlk  ds
     d  LMetaDataLen                 10i 0
     d  BStrRunTimeV                 32A
     d  BStrMethodNA                 32A
     d  BStrProgID                   40A
     d  BStrClsID                    40A
     d  UsMajorVer                    5i 0
     d  UsMinorVersin                 5i 0
     d  SReadyToComt                  5i 0
     d  SWillingToDoM                 5i 0
     d  SRetrnErrToCl                 5i 0
     d  SErrorCode                    5i 0
     d  LHelpContext                 10i 0
     d  BstrHelpStr                 256A
      *
      ******************************************************************
      * Transaction Detail
      ******************************************************************
      *
     D dsTxnDetails    ds
     d  TxnSSN                        9A
     d  TxnAcctNum                   10A
     d  TxnItemNum                    7p 0
     d  TxnType                       1A
     d  TxnDate                      10A
     d  TxnAmount                    15p 2
      *
      ******************************************************************
      * Constants.
      ******************************************************************
      *
     d cTxnTypIntlBal  c                   CONST('B')
     d cTxnTypCredit   c                   CONST('C')
     d cTxnTypDebit    c                   CONST('D')
     d cTxnTypSvcChg   c                   CONST('S')
     d cTxnTypOdChg    c                   CONST('O')
     D cWBCustDbDD     C                   CONST('WBCUSTDB')
     D cWBAcctDbDD     C                   CONST('WBACCTDB')
     D cWBTxnDbDD      C                   CONST('WBTXNDBB')
      *
     C     *ENTRY        PLIST
     c                   PARM                    pMetaDtaErrBlk
     c                   PARM                    pCustName
     c                   PARM                    pUserData
      *
     C     kyCusMas1     KLIST
     c                   KFLD                    pCustName
      *
      * Initialize return codes.
      *
     C                   Z-ADD     *ZEROS        RetCode
     C                   Z-ADD     *ZEROS        SErrorCode
     c                   z-add     *ZEROS        SRetrnErrToCl
      *
      *Use the customer name to access the customer master file
      *
     C                   exsr      GetCust
      *
     c                   SELECT
     c                   WHEN      *IN99          = *ON
     c                   EXSR      UpdCustNotFnd
      *
     c                   WHEN      *IN99          = *OFF
     c                   EXSR      LoadCustInfo
      *
     c                   WHEN      *IN98          = *ON
     c                   EXSR      UpdCustError
      *
     c                   OTHER
     c                   EXSR      UpdCustError
     c                   ENDSL
     C                   eval      *INLR         = *ON
      *
      **************************************************************
      * Get Customer Account Information.
      **************************************************************
      *
     c     GETCUST       begsr
      *
     c                   eval      *IN99          = *OFF
     c     kyCusMas1     setll     cusmasl1
     c     kyCusMas1     reade     cusmasl1                             9899
      *
     c                   endsr
      *
      **************************************************************
      * Customer account found load output fields
      **************************************************************
      *
     c     LOADCUSTINFO  begsr
      *
     C                   z-add     *ZEROS        RetCode
     C                   z-add     *ZEROS        SERRORCODE
     c                   move      *BLANKS       BSTRHELPSTR
     c                   eval      pCustomerName =CUSNAM
     c                   eval      pCustomerSSN  =CUSSSN
     c                   eval      pCustomerStr  =CUSSTR
     c                   eval      pCustomerCity =CUSCTY
     c                   eval      pCustomerSta  =CUSSTA
     c                   z-add     CUSZIP        pCustomerZip
     c                   eval      pCustomerPhne =CUSPHO
     c                   eval      pCustomerPin  =CUSPIN
      *
     c                   endsr
      *
      **************************************************************
      * Customer account not open.
      **************************************************************
      *
     c     UPDCUSTNOTOPENbegsr
      *
     c                   MOVE      *BLANKS       BStrHelpStr
     c                   MOVEL     msg(1)        BSTRHELPSTR
     c                   z-add     1             SRetrnErrToCl
     c                   z-add     5001          SErrorCode
     c                   z-add     5001          RetCode
      *
     c                   endsr
      *
      **************************************************************
      * Customer account not found.
      **************************************************************
      *
     c     UPDCUSTNOTFND begsr
      *
     c                   MOVE      *BLANKS       BStrHelpStr
     c                   MOVEL     msg(2)        BStrHelpStr
     c                   z-add     1             SRetrnErrToCl
     c                   z-add     5002          SErrorCode
     c                   z-add     5002          RetCode
      *
     c                   endsr
      *
      **************************************************************
      * Customer error.
      **************************************************************
      *
     c     UPDCUSTERROR  begsr
      *
     c                   MOVE      *BLANKS       BStrHelpStr
     c                   MOVEL     msg(3)        BStrHelpStr
     c                   z-add     1             SRetrnErrToCl
     c                   z-add     5003          SErrorCode
     c                   z-add     5003          pRetCode
      *
     c                   endsr
**
Customer file not open                                                 ING
Customer name not found
I/O Error Customer file
