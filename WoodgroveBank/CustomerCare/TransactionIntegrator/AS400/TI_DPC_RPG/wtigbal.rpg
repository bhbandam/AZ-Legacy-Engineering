      ******************************************************************
      * File Definitions
      ******************************************************************
      *
     FCusMasL1  if   e           k DISK
     FActMasL2  if   e           k DISK
     FTrnDtlL1  if   e           k DISK
      *
      ******************************************************************
      * Data Definitions
      ******************************************************************
     D Name            s             30A
     D RespCode        S              9S 0 INZ(*ZEROS)
     D RetCode         S              4  0 INZ(*ZEROS)
     D EditNum         S              7S 0
     D msg             S             45A   Dim(3) ctdata
     D #cnt            S              3  0
      *
     D SSN             s                   LIKE(CUSSSN)
     D Num             s                   LIKE(actnum)
     d pRetCode        s              4  0
     d pCustNameIn     s             30A
     d pCustName       s             30A
     d pCustAcct       s                   LIKE(actnum)
     d pAcctBal        s             15p 2
      *
      ******************************************************************
      * Constants.
      ******************************************************************
      *
     d cTXNTYPINTLBAL  c                   CONST('B')
     d cTXNTYPCREDIT   c                   CONST('C')
     d cTXNTYPDEBIT    c                   CONST('D')
     d cTXNTYPSVCCHG   c                   CONST('S')
     d cTXNTYPODCHG    c                   CONST('O')
     D cWBCUSTDBDD     C                   CONST('WBCUSTDB')
     D cWBACCTDBDD     C                   CONST('WBACCTDB')
     D cWBTXNDBDD      C                   CONST('WBTXNDBB')
      *
      ******************************************************************
      * Account information muliple occurance data structure.
      ******************************************************************
      *
     d dsAccts         ds                  QUALIFIED
     d  dsActNum                     10A
     d  dsActType                     1A
     d  dsActName                    10A
     d  dsAcctInfo                         LIKEDS(dsActInfo)
      *
      ******************************************************************
      * Account information used to redefine the account area.
      ******************************************************************
      *
     d dsActTypCheck   ds                  QUALIFIED
     d  ActChkOdChg                   5P 2
     d  ActChkOdLimit                 7P 2
     d  ActChkOdLnk                  10A
     d  ActChkLastStm                10a
     d  ActChkDtlItem                 7P 0
     d  ActChkBal                    15P 2
      *
     d dsActTypSave    ds                   QUALIFIED
     d  ActSavIntRte                  3p 2
     d  ActSavSrvChg                  5p 2
     d  ActSavLstStm                 10a
     d  ActSavDtlItms                 7p 0
     d  ActSavBal                    15p 2
      *
     d dsActInfo       ds                  QUALIFIED
     d  dsActAre                     39A
     d  dsActChk                           LIKEDS(dsActTypCheck)
     d                                     OVERLAY(dsActAre:1)
      *
     d  dsActSav                           LIKEDS(dsActTypSave)
     d                                     OVERLAY(dsActAre:1)
      *
      ******************************************************************
      * Data structure - Transaction Detail
      ******************************************************************
      *
     D dsTxnDetails    ds
     d  TxnSSN                        9A
     d  TxnAcctNum                   10A
     d  TxnItemNum                    7p 0
     d  TxnType                       1A
     d  TxnDate                      10A
     d  TxnAmount                    15p 2
      *
      ******************************************************************
      *Data structure -  Metadata Block
      ******************************************************************
      *
     d pMetaDtaErrBlk  ds
     d  LMetaDataLen                 10i 0
     d  BStrRunTimeV                 32A
     d  BStrMethodNA                 32A
     d  BStrProgID                   40A
     d  BStrClsID                    40A
     d  UsMajorVer                    5i 0
     d  UsMinorVersin                 5i 0
     d  SReadyToComt                  5i 0
     d  SWillingToDoM                 5i 0
     d  SRetrnErrToCl                 5i 0
     d  SErrorCode                    5i 0
     d  LHelpContext                 10i 0
     d  BstrHelpStr                 256A
      *
     C     *ENTRY        PLIST
     c                   PARM                    pMetaDtaErrBlk
     c                   PARM                    pAcctbal
     c                   PARM                    pCustName
     c                   PARM                    pCustAcct
      *
      * Initialize return codes.
      *
     C                   Z-ADD     *ZEROS        RetCode
     C                   Z-ADD     *ZEROS        SErrorCode
     c                   z-add     *ZEROS        SRetrnErrToCl
      *
      *Use the customer name to access the customer master file
      *
     C                   exsr      GetCust
      *
     c                   SELECT
     c                   WHEN      *IN99          = *ON
     c                   EXSR      UPDCUSTNOTFND
      *
     c                   WHEN      *IN99          = *OFF
     c                   EXSR      GetAct
      *
     c                   WHEN      *IN98          = *ON
     c                   EXSR      UPDCUSTERROR
      *
     c                   OTHER
     c                   EXSR      UPDCUSTERROR
     c                   ENDSL
     C                   eval      *INLR         = *ON
      *
      **************************************************************
      * Get Customer Account Information.
      **************************************************************
      *
     c     GETCUST       begsr
      *
     c                   eval      *IN99          = *OFF
     c     pCustName     setll     CusMasL1
     c     pCustName     reade     CusMasL1                             9899
      *
     c                   endsr
      *
      **************************************************************
      * Get Account Information.
      **************************************************************
      *
     c     GETACT        begsr
      *
     c     k1ActKey      klist
     c                   kfld                    ssn
     c                   kfld                    actnum 
      *
      * Load key fields and initialize indicators
      *
     C                   eval      ssn           = CusSSN
     C                   eval      actnum        = pCustAcct     
     c                   eval      *IN99          = *OFF
      *
     c     k1Actkey      setll     ActMasl2
     c     k1Actkey      reade     ActMasl2                             9899
      *
     c                   if        *IN99          = *OFF
     C                   eval      dsActInfo.dsActAre =ActAre
     c                   eval      pCustAcct          =actnum
     C                   z-add     *zeros        bal              15 2
      *
     C                   eval      dsAccts.dsActNum  = actnum
     C                   eval      dsAccts.dsActType = acttyp     
     C                   eval      dsAccts.dsActName = actnam
      *
     c                   select
     c                   WHEN      Acttyp          = 'C'
     C                   eval      pAcctBal=dsActInfo.dsActChk.ActChkBal
      *
     c                   WHEN      Acttyp          = 'S'
     C                   eval      pAcctBal=dsActInfo.dsActSav.ActSavBal
      *
     C                   OTHER
     c                   endsl
      *
     c                   endif
      *
     c                   endsr
      *
      **************************************************************
      * Customer account not open.
      **************************************************************
      *
     c     UPDCUSTNOTOPENbegsr
      *
     c                   MOVEL     msg(1)        BSTRHELPSTR
     c                   z-add     1             SRetrnErrToCl
     c                   z-add     5001          SERRORCODE
     c                   z-add     5001          RETCODE
      *
     c                   endsr
      *
      **************************************************************
      * Customer account not found.
      **************************************************************
      *
     c     UPDCUSTNOTFND begsr
      *
     c                   MOVEL     msg(2)        BSTRHELPSTR
     c                   z-add     1             SRetrnErrToCl
     c                   z-add     5002          SERRORCODE
     c                   z-add     5002          RETCODE
      *
     c                   endsr
      *
      **************************************************************
      * Customer error.
      **************************************************************
      *
     c     UPDCUSTERROR  begsr
      *
     c                   MOVEL     msg(3)        BSTRHELPSTR
     c                   z-add     1             SRetrnErrToCl
     c                   z-add     5003          SERRORCODE
     c                   z-add     5003          RETCODE
      *
     c                   endsr
**
Customer file not open
Customer name not found
I/O Error Customer file
